allprojects {
    repositories {
        mavenCentral()
    }

    group = 'com.andrewkroh.cisco'
    version = '1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.6

    // Code can compile under 1.6 but MulticastChannel
    // used by Netty will not be available and multicast
    // features will not work.
    targetCompatibility = 1.6

    project.ext.fullProjectName = rootProject.name + '-' + project.name

    jar {
        manifest {
            attributes 'Implementation-Title': project.ext.fullProjectName,
                       'Implementation-Version': version,
                       'Built-By': System.getProperty('user.name'),
                       'Built-Date': new Date(),
                       'Built-JDK': System.getProperty('java.version'),
                       'Built-Gradle': gradle.gradleVersion
        }
    }

    dependencies {
        testCompile 'org.mockito:mockito-all:1.9.5',
                    'junit:junit:4.+',
                    'org.hamcrest:hamcrest-all:1.3'
    }

    // Override the names of generated archives:
    project.tasks.withType(AbstractArchiveTask).each {
        AbstractArchiveTask task ->
        task.baseName = project.ext.fullProjectName
    }

    eclipse.project.name = project.ext.fullProjectName
}


// --------------------------
// Subproject configurations:
// --------------------------
project(':common') {
    dependencies {
        compile 'com.google.guava:guava:15.0'
    }
}

project(':multicast-listener') {
    apply plugin: 'application'

    mainClassName = 'com.andrewkroh.cisco.multicast.MulticastListener'

    dependencies {
        compile 'com.beust:jcommander:1.29',
                'org.slf4j:slf4j-api:1.7.5',
                project(':common')
        runtime 'org.slf4j:slf4j-simple:1.7.5'
    }
}

project(':rtp-streaming') {
    apply plugin: 'application'

    mainClassName = 'com.andrewkroh.cicso.rtp.AudioFileStreamerMain'

    dependencies {
        compile 'com.beust:jcommander:1.29',
                'org.slf4j:slf4j-api:1.7.5',
                'org.apache.commons:commons-lang3:3.1',
                'io.netty:netty-all:4.0.12.Final',
                'org.apache.commons:commons-io:1.3.2',
                project(':common')
        runtime 'org.slf4j:slf4j-simple:1.7.5'
    }

    test {
        jvmArgs '-Djava.net.preferIPv4Stack=true'
    }
}

project(':xml-services-api') {
    apply plugin: 'java'

    // Cisco version from the XSD:
    version = '3.3.4'

    configurations {
        xjc
    }

    dependencies {
        compile 'com.google.guava:guava:15.0'

        xjc 'com.sun.xml.bind:jaxb-impl:2.2+',
            'com.sun.xml.bind:jaxb-xjc:2.2+'
    }
 
    sourceSets {
        generated {
            java.srcDirs = ["$buildDir/generated-src/jaxb"]
        }
    }

    task xjc {
        ext.package = 'com.cisco.xmlservices.generated'
        ext.outputDir = sourceSets.generated.java.srcDirs.iterator().next()
        outputs.dir(ext.outputDir)

        doLast {
            ant.taskdef(name: 'xjc', 
                        classname: 'com.sun.tools.xjc.XJCTask',
                        classpath: configurations.xjc.asPath)

            ext.outputDir.mkdirs()
            ant.xjc(destdir: ext.outputDir, package: ext.package,
                    removeOldOutput: 'yes') {
                schema(dir: 'src/main/resources/schema') {
                    include (name: 'CiscoIPPhone.xsd')
                }
                arg(value: '-verbose')
            }
        }
    }
 
    compileJava.dependsOn xjc
    compileJava.source xjc.outputs.files, sourceSets.main.java
}

